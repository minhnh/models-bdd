# SPDX-License-Identifier:  MPL-2.0
PREFIX bhv: <https://secorolab.github.io/metamodels/behaviour#>
PREFIX bdd: <https://secorolab.github.io/metamodels/acceptance-criteria/bdd#>

CONSTRUCT {
    ?scenarioExec a ?execType ;
        bdd:of-variant ?scenarioVar ;
        bdd:of-template ?scenarioTmpl ;
        bdd:has-behaviour-impl ?bhvImpl .
    ?bhvImpl a ?bhvImplType ;
        bhv:of-behaviour ?behaviour .
    ?behaviour a ?bhvType .
}
WHERE {
    ?scenarioExec a bdd:ScenarioExecution ;
        a ?execType ;
        bdd:of-variant ?scenarioVar ;
        bdd:has-behaviour-impl ?bhvImpl .

    ?scenarioVar a bdd:ScenarioVariant ;
        bdd:of-template ?scenarioTmpl .

    ?scenarioTmpl a bdd:ScenarioTemplate ;
        bdd:of-scenario / bhv:of-behaviour ?behaviour .

    ?bhvImpl
        a bdd:BehaviourImplementation ;
        a ?bhvImplType ;
        bhv:of-behaviour ?behaviour .

    ?behaviour
        a bhv:Behaviour ;
        a ?bhvType .
}
